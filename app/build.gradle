plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.2.0'
}

android {
    namespace 'io.github.bradpatras.justworkout'
    compileSdk 36

    defaultConfig {
        applicationId "io.github.bradpatras.justworkout"
        minSdk 31
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin#pre-release_kotlin_compatibility
        kotlinCompilerExtensionVersion '1.5.15'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

dependencies {
    // android stuff
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7"
    implementation 'androidx.activity:activity-compose:1.10.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.5'

    // compose
    // https://developer.android.com/jetpack/compose/bom/bom-mapping
    implementation platform('androidx.compose:compose-bom:2025.07.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    // timber logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // navigation annotations
    def destinations_version = '2.2.0'
    implementation "io.github.raamcosta.compose-destinations:core:$destinations_version"
    implementation "io.github.raamcosta.compose-destinations:bottom-sheet:$destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$destinations_version" // broken? check: https://github.com/google/ksp/issues/1401

    // room db
    def room_version = '2.7.2'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // hilt
    // If you run into issues with hilt and ksp check this thread https://github.com/google/dagger/issues/3965
    def hilt_version = '2.57'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"

    // drag and drop list
    implementation 'sh.calvin.reorderable:reorderable:2.5.1'

    // testing/debug
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.07.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}